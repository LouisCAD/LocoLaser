apply plugin: 'java'
apply plugin: 'application'
apply plugin: "maven-publish"
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.7
version = '1.0.0'
mainClassName = "ru.pocketbyte.locolaser.Main"

repositories {
    mavenCentral()
}

ext {
    locolaserModules = [
        project(':core'),
        project(':source-googlesheet'),
        project(':platform-mobile')
    ]
}

dependencies {
    compile locolaserModules

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    manifest {
        attributes('Main-Class': "$mainClassName")
    }
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
}

// Load credentials for bintray publication
File propFile = project.rootProject.file('local.properties')
if (propFile.exists()) {
    Properties props = new Properties()
    props.load(propFile.newDataInputStream())
    ext {
        bintrayUser = props.get("bintray.user")
        bintrayApikey = props.get("bintray.apikey")
        bintrayGpgPassphrase = props.get("bintray.gpgPassphrase")
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

task generateJavadocs(type: Javadoc) {
    source {
        sourceSets.main.allJava

        locolaserModules.collect {
            it.sourceSets.main.allJava
        }
    }
    failOnError = false
}

task sourceDoc(type: Jar, dependsOn: generateJavadocs) {
    from javadoc.destinationDir
}

publishing {
    publications {
        locolaserPublication(MavenPublication) {
            from components.java
            groupId 'ru.pocketbyte.locolaser'
//            artifactId 'locolaser'
            artifactId 'locolaser-mobile-googlesheet'
            version project.version
            pom.withXml {
                def root = asNode()
                root.remove(root.get('dependencies'))
            }
            artifact sourceJar {
                classifier "sources"
            }
            artifact sourceDoc {
                classifier "javadoc"
            }
        }
    }
}

bintray {
    publish = true
    override = true

    user = bintrayUser
    key = bintrayApikey
    pkg {
        repo = 'maven'
        name = 'locolaser-mobile-googlesheet'
        userOrg = 'pocketbyte'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/PocketByte/LocoLaser.git'
        websiteUrl = 'https://github.com/PocketByte/LocoLaser'
        issueTrackerUrl = 'https://github.com/PocketByte/LocoLaser/issue'
        version {
            name = project.version
            desc = 'Tool for Android and iOS to import localized strings from Google Sheets.'
            released  = new Date()
            vcsTag = project.version
            gpg.passphrase = bintrayGpgPassphrase
        }
        publications = ['locolaserPublication']
    }
}